#lang "qb"
10 REM
20 REM LOGO VERSION 2
30 REM WRITTEN 21.1.86
40 REM BY PMC
50 REM
60 DIM M$(12)
65 DIM A%(290),B%(10)
70 SCREEN 1:COLOR 1,1
71 CLS
75 'KEY OFF
76 LOCATE 2,6
77 PRINT "SHIELD LIFE INSURANCE COMPANY"
78 FOR X=0 TO 28
79 GET ((5+X)*8,8)-((5+X)*8+7,15),B%
80 FOR Y=0 TO 9
81 A%(X*10+Y+1)=B%(Y)
82 NEXT Y
83 NEXT X
86 MC$="I"
87 FOR X=0 TO 319
88 LINE (X,0)-(X,199),1
89 NEXT X
90 SIZ=70:XC=160:YC=96
100 CDB=1:CNB=9:CBB=11:CW=3
110 IF MC$="W" THEN COB=15
120 IF MC$="I" THEN COB=3:CDB=0
130 LINE (16,4)-(304,19),0,BF
131 FOR X=0 TO 28
132 FOR Y=0 TO 9
133 B%(Y)=A%(X*10+Y+1)
134 NEXT Y
135 FOR Y=264 TO (5+X)*8 STEP -1
136 PUT (Y,8),B%,PSET
137 NEXT Y
138 NEXT X
140 REM STEP 1:OUTER BORDER IN WHITE -----------------------------------
150 FOR A=0 TO 360 STEP 30
160 A1=A:GOSUB 10000
170 IF A=0 THEN LINE (XC,YC)-(XC+C*SIZ*.71,YC+S*SIZ*.71),CW:GOTO 230
180 LINE -(XC+C*SIZ,YC+S*SIZ),CW
190 LINE (XC,YC)-(XC+C*SIZ,YC+S*SIZ),CW
195 X=XC+C*SIZ*.71:Y=YC+S*SIZ*.71
200 A1=A-15:GOSUB 10000
210 PAINT (XC+C*SIZ*.2,YC+S*SIZ*.2),CW,CW
220 PSET (X,Y),CW
230 NEXT A
240 REM STEP 2:NEXT BLOCK IN BLUE ---------------------------------------
250 CIRCLE (XC,YC),SIZ*.42,CDB,,,1
260 FOR A=0 TO 360 STEP 30
270 A1=A-3:GOSUB 10000
280 IF A=0 THEN 340
290 LINE -(XC+C*SIZ*.9,YC+S*SIZ*.9),CDB
300 A1=A-6:GOSUB 10000
310 LINE -(XC+C*SIZ*.42,YC+S*SIZ*.42),CDB
320 A1=A-15:GOSUB 10000
330 PAINT (XC+C*SIZ*.5,YC+S*SIZ*.5),CDB,CDB
340 A1=A:GOSUB 10000
350 LINE (XC+C*SIZ*.42,YC+S*SIZ*.42)-(XC+C*SIZ*.66,YC+S*SIZ*.66),CDB
360 NEXT A
370 REM STEP 3:CORRECT INNER CIRCLE ------------------------------------
380 CIRCLE (XC,YC),SIZ*.42,CW,,,1
410 REM STEP 4:WHITE INDENTS -------------------------------------------
420 FOR A=0 TO 360 STEP .5
430 A1=A:GOSUB 10000
440 REM LINE (XC,YC)-(XC+C*SIZ*.42,YC+S*SIZ*.42),CW
450 Z=A/30-INT(A/30)
460 Z=INT(Z*30)
470 IF Z<22 THEN PSET (XC+C*SIZ*.66,YC+S*SIZ*.66),CW:GOTO 530
480 IF Z>22 THEN 530
490 A1=A-22:GOSUB 10000
500 LINE -(XC+C*SIZ*.42,YC+S*SIZ*.42),CW
510 A1=A-7:GOSUB 10000
520 PAINT (XC+C*SIZ*.6,YC+S*SIZ*.6),CW,CW
530 NEXT A
540 REM STEP 5:INNER-MOST CIRCLE ---------------------------------------
550 CIRCLE (XC,YC),SIZ*.38,CDB,,,1
560 PAINT (XC,YC),CDB,CDB
570 REM STEP 6:THE S ---------------------------------------------------
580 OTR=1:INR=1
590 FOR A=50 TO 180 STEP .5
600 A1=A:GOSUB 10000
610 LINE (XC+C*SIZ*.17*INR,YC+S*SIZ*.17*INR)-(XC+C*SIZ*.35*OTR,YC+S*SIZ*.35*OTR),CW
620 LINE (XC-C*SIZ*.17*INR,YC-S*SIZ*.17*INR)-(XC-C*SIZ*.35*OTR,YC-S*SIZ*.35*OTR),CW
630 IF A<>INT(A) THEN 670
640 IF A<110 THEN INR=INR+.006:OTR=OTR-.002
650 IF A>110 THEN INR=INR-.02:OTR=OTR-8.000001E-03
660 IF A>120 THEN INR=0
670 NEXT A
680 FOR A=0 TO 120 STEP .5
690 A1=A:GOSUB 10000
700 LINE (XC,YC)-(XC+C*SIZ*.35*OTR,YC+S*SIZ*.35*OTR),CW
710 LINE (XC,YC)-(XC-C*SIZ*.35*OTR,YC-S*SIZ*.35*OTR),CW
720 IF A<>INT(A) THEN 770
730 IF A<40 THEN OTR=OTR*.99
740 IF A>40 THEN OTR=OTR*1
750 IF A>86 THEN OTR=OTR*1.02
760 IF A>114 THEN OTR=OTR*1.04
770 NEXT A
780 REM STEP 7:TEXT ----------------------------------------------------
790 LOCATE 3,4:PRINT "Shield"
800 LOCATE 4,5:PRINT "Life"
810 LOCATE 3,32:PRINT "Shield"
820 LOCATE 4,33:PRINT "Life"
830 DY$=MID$(DATE$,4,2):MN$=LEFT$(DATE$,2)
840 YR$=RIGHT$(DATE$,4)
850 FOR X=1 TO 12:READ M$(X):NEXT X
860 DATA " January "," February","  March  ","  April  ","   May   ","   June  "
870 DATA "   July  ","  August ","September"," October "," November"," December"
880 LOCATE 21,2:PRINT M$(VAL(MN$))
890 LOCATE 22,3:PRINT DY$;",";YR$
900 REM STEP 8:FIRST 30 SECOND DISPLAY ---------------------------------
910 OSC=VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))*60:K$=""
920 WHILE K$="" AND SC<OSC+11
930 IF TIME$<>OTIME$ THEN LOCATE 21,31:OTIME$=TIME$:PRINT OTIME$:SC=VAL(RIGHT$(OTIME$,2))+VAL(MID$(OTIME$,4,2))*60
940 K$=INKEY$
950 WEND
955 REM STEP 9: CLOCK MODE ---------------------------------------------
960 PAINT (XC+SIZ*.35,YC),CW,CW
970 OH=99:OM=99:OS=99
980 WHILE K$=""
990 IF TIME$=OTIME$ THEN 990
1000 OTIME$=TIME$
1010 H=VAL(LEFT$(OTIME$,2)):IF H>12 THEN H=H-12
1020 M=VAL(MID$(OTIME$,4,2))
1025 H=INT(((H*60+M)/60)*5)
1030 SC=VAL(RIGHT$(OTIME$,2))
1040 IF H<>OH THEN COL=CW:VL=OH:SZ=.2:GOSUB 11000
1050 IF M<>OM THEN COL=CW:VL=OM:SZ=.25:GOSUB 11000
1060 IF SC<>OS THEN COL=CW:VL=OS:SZ=.3:GOSUB 11000
1061 COL=1:VL=H:SZ=.2:GOSUB 11000
1063 COL=9:VL=M:SZ=.25:GOSUB 11000
1065 COL=11:VL=SC:SZ=.3:GOSUB 11000
1066 OH=H:OM=M:OS=SC
1068 LOCATE 21,31:PRINT OTIME$
1069 K$=INKEY$
1070 WEND
1080 COLOR 7,0
1090 SCREEN 2
1200 END
10000 A2=A1*6.28/360
10010 S=-SIN(A2):C=COS(A2)
10020 RETURN
11000 REM DRAW CLOCK HAND
11010 IF VL=99 THEN 11060
11020 A1=(60-VL)*6+90:GOSUB 10000
11030 LINE (XC,YC)-(XC+C*SIZ*SZ,YC+S*SIZ*SZ),COL
11060 RETURN
