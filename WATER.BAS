#lang "qb"
100 REM
110 REM LOGO VERSION 3
120 REM WRITTEN 23.5.87
130 REM BY PMC
140 REM
150 DIM A%(20)
160 REM SET COLOURS AND DISPLAY TITLE SCREEN ------------------------
170 SCREEN 1:COLOR 1,1
180 CLS
190 KEY OFF
200 LOCATE 12,6
210 PRINT "SHIELD LIFE INSURANCE COMPANY"
220 LOCATE 13,9
230 PRINT "Working for the future"
240 LOCATE 14,8
250 PRINT "by working professionally"
260 REM WAIT FOR FOUR SECONDS ---------------------------------------
270 ELSC=0:ST$=TIME$
280 WHILE ELSC<5
290    IF TIME$<>ST$ THEN ELSC=ELSC+1:ST$=TIME$
300 WEND
310 REM SOFT SCROLL OF TITLES ---------------------------------------
320 FOR X=0 TO 28
330 GET ((5+X)*8,11*8)-((5+X)*8+7,11*8+7),A%
340 FOR Y=11*8 TO 1*8 STEP-1
350 PUT ((5+X)*8,Y),A%,PSET
360 NEXT Y
370 NEXT X
380 FOR X=0 TO 24
390 GET ((7+X)*8,13*8)-((7+X)*8+7,13*8+7),A%
400 PUT ((7+X)*8,13*8+1),A%,PSET
410 LINE ((7+X)*8,13*8)-((7+X)*8+7,13*8),0
420 GET ((7+X)*8,13*8)-((7+X)*8+7,13*8+8),A%
430 FOR Y=13*8 TO 23*8
440 PUT ((7+X)*8,Y),A%,PSET
450 NEXT Y
460 NEXT X
470 FOR X=0 TO 21
480 GET ((8+X)*8,12*8-1)-((8+X)*8+7,12*8+7),A%
490 FOR Y=12*8 TO 22*8-1
500 PUT ((8+X)*8,Y),A%,PSET
510 NEXT Y
520 NEXT X
530 MC$="I"
535 GOSUB 20000
540 FOR X=0 TO 319
550 LINE (X,20)-(X,171),1
560 NEXT X
570 SIZ=70:XC=160:YC=96
580 CDB=1:CW=3
590 IF MC$="I" THEN CDB=0
600 REM STEP 1-OUTER BORDER IN WHITE -----------------------------------
610 FOR A=0 TO 360 STEP 30
620 A1=A:GOSUB 10000
630 IF A=0 THEN LINE (XC,YC)-(XC+C*SIZ*.71,YC+S*SIZ*.71),CW:GOTO 700
640 LINE -(XC+C*SIZ,YC+S*SIZ),CW
650 LINE (XC,YC)-(XC+C*SIZ,YC+S*SIZ),CW
660 X=XC+C*SIZ*.71:Y=YC+S*SIZ*.71
670 A1=A-15:GOSUB 10000
680 PAINT (XC+C*SIZ*.2,YC+S*SIZ*.2),CW,CW
690 PSET (X,Y),CW
700 NEXT A
710 REM STEP 2-NEXT BLOCK IN BLUE ---------------------------------------
720 CIRCLE (XC,YC),SIZ*.42,CDB,,,1
730 FOR A=0 TO 360 STEP 30
740 A1=A-3:GOSUB 10000
750 IF A=0 THEN 810
760 LINE -(XC+C*SIZ*.9,YC+S*SIZ*.9),CDB
770 A1=A-6:GOSUB 10000
780 LINE -(XC+C*SIZ*.42,YC+S*SIZ*.42),CDB
790 A1=A-15:GOSUB 10000
800 PAINT (XC+C*SIZ*.5,YC+S*SIZ*.5),CDB,CDB
810 A1=A:GOSUB 10000
820 LINE (XC+C*SIZ*.42,YC+S*SIZ*.42)-(XC+C*SIZ*.66,YC+S*SIZ*.66),CDB
830 NEXT A
840 REM STEP 3-CORRECT INNER CIRCLE ------------------------------------
850 CIRCLE (XC,YC),SIZ*.42,CW,,,1
860 REM STEP 4:WHITE INDENTS -------------------------------------------
870 FOR A=0 TO 360 STEP .5
880 A1=A:GOSUB 10000
890 Z=A/30-INT(A/30)
900 Z=INT(Z*30)
910 IF Z<22 THEN PSET (XC+C*SIZ*.66,YC+S*SIZ*.66),CW:GOTO 970
920 IF Z>22 THEN 970
930 A1=A-22:GOSUB 10000
940 LINE -(XC+C*SIZ*.42,YC+S*SIZ*.42),CW
950 A1=A-7:GOSUB 10000
960 PAINT (XC+C*SIZ*.6,YC+S*SIZ*.6),CW,CW
970 NEXT A
980 REM STEP 5-INNER-MOST CIRCLE ---------------------------------------
990 CIRCLE (XC,YC),SIZ*.38,CDB,,,1
1000 PAINT (XC,YC),CDB,CDB
1010 REM STEP 6-THE S ---------------------------------------------------
1020 OTR=1:INR=1
1030 FOR A=50 TO 180 STEP .5
1040 A1=A:GOSUB 10000
1050 LINE (XC+C*SIZ*.17*INR,YC+S*SIZ*.17*INR)-(XC+C*SIZ*.35*OTR,YC+S*SIZ*.35*OTR),CW
1060 LINE (XC-C*SIZ*.17*INR,YC-S*SIZ*.17*INR)-(XC-C*SIZ*.35*OTR,YC-S*SIZ*.35*OTR),CW
1070 IF A<>INT(A) THEN 1110
1080 IF A<110 THEN INR=INR+.006:OTR=OTR-.002
1090 IF A>110 THEN INR=INR-.02:OTR=OTR-8.000001E-03
1100 IF A>120 THEN INR=0
1110 NEXT A
1120 FOR A=0 TO 120 STEP .5
1130 A1=A:GOSUB 10000
1140 LINE (XC,YC)-(XC+C*SIZ*.35*OTR,YC+S*SIZ*.35*OTR),CW
1150 LINE (XC,YC)-(XC-C*SIZ*.35*OTR,YC-S*SIZ*.35*OTR),CW
1160 IF A<>INT(A) THEN 1210
1170 IF A<40 THEN OTR=OTR*.99
1180 IF A>40 THEN OTR=OTR*1
1190 IF A>86 THEN OTR=OTR*1.02
1200 IF A>114 THEN OTR=OTR*1.04
1210 NEXT A
1220 REM STEP 7- DISPLAY DATE -------------------------------------------
1230 LINE (16,28)-(104,52),0,BF
1240 DY$=MID$(DATE$,4,2):MN$=LEFT$(DATE$,2)
1250 YR$=RIGHT$(DATE$,4)
1255 RESTORE 1290
1260 FOR X=1 TO VAL(MN$)
1270     READ M$
1280 NEXT X
1290 DATA " January "," February","  March  ","  April  ","   May   ","   June  "
1300 DATA "   July  ","  August ","September"," October "," November"," December"
1310 LOCATE 5,4:PRINT M$;
1320 LOCATE 6,5:PRINT DY$;",";YR$
1330 REM STEP 8-FIRST 10 SECOND DISPLAY ---------------------------------
1340 LINE (224,148)-(304,164),0,BF
1350 OSC=VAL(RIGHT$(TIME$,2))+VAL(MID$(TIME$,4,2))*60:K$=""
1360 WHILE K$="" AND SC<OSC+11
1370 IF TIME$<>OTIME$ THEN LOCATE 20,30:OTIME$=TIME$:PRINT OTIME$:SC=VAL(RIGHT$(OTIME$,2))+VAL(MID$(OTIME$,4,2))*60
1380 K$=INKEY$
1390 WEND
1400 REM STEP 9- CLOCK MODE ---------------------------------------------
1410 PAINT (XC+SIZ*.35,YC),CW,CW
1420 OH=99:OM=99:OS=99:ELSC=0
1430 WHILE K$="" AND ELSC<30
1440 IF TIME$=OTIME$ THEN 1440
1445 ELSC=ELSC+1
1450 OTIME$=TIME$
1460 H=VAL(LEFT$(OTIME$,2)):IF H>12 THEN H=H-12
1470 M=VAL(MID$(OTIME$,4,2))
1480 H=INT(((H*60+M)/60)*5)
1490 SC=VAL(RIGHT$(OTIME$,2))
1500 IF H<>OH THEN COL=CW:VL=OH:SZ=.15:GOSUB 10030
1510 IF M<>OM THEN COL=CW:VL=OM:SZ=.25:GOSUB 10030
1520 IF SC<>OS THEN COL=CW:VL=OS:SZ=.35:GOSUB 10030
1530 COL=0:VL=H:SZ=.15:GOSUB 10030
1540 COL=1:VL=M:SZ=.25:GOSUB 10030
1550 COL=2:VL=SC:SZ=.35:GOSUB 10030
1560 OH=H:OM=M:OS=SC
1570 LOCATE 20,30:PRINT OTIME$
1580 K$=INKEY$
1590 WEND
1595 IF K$="" THEN 170
1600 SCREEN 0:WIDTH 80
1610 COLOR 7,0
1620 END
10000 A2=A1*6.28/360
10010 S=-SIN(A2):C=COS(A2)
10020 RETURN
10030 REM DRAW CLOCK HAND
10040 IF VL=99 THEN 10070
10050 A1=(60-VL)*6+90:GOSUB 10000
10060 LINE (XC,YC)-(XC+C*SIZ*SZ,YC+S*SIZ*SZ),COL
10070 RETURN
20000 REM WATERFORD TEXT
20010 T$="W E L C O M E"
20020 ROW=10:GOSUB 21000
20030 T$="to the opening of our new"
20040 ROW=12:GOSUB 21000
20050 T$="Waterford Branch Office"
20060 ROW=14:GOSUB 21000
20061 LINE (3*8,7*8)-(36*8,16*8),1,B
20062 LINE (3*8,8*8)-(36*8,15*8),1,B
20063 PAINT (3*8+1,7*8+1),1
20064 PAINT (3*8+1,15*8+1),1
20070 ELSC=0:ST$=""
20080 WHILE ELSC<10
20090    IF TIME$<>ST$ THEN ELSC=ELSC+1:ST$=TIME$
20100 WEND
20110 FOR X=3*8 TO 36*8
20120    IF X<36*8 THEN LINE (X+1,7*8)-(X+1,16*8),1
20125    LINE (X,7*8)-(X,16*8),0
20130 NEXT X
20200 RETURN
21000 L=LEN(T$)
21010 COL=20-INT(L/2)
21020 XT=(COL-1)*8
21030 YT=(ROW-1)*8
21040 FOR Z=1 TO L
21050 C$=MID$(T$,Z,1)
21055 IF C$=" " THEN 21155
21060 LOCATE 20,40:PRINT C$;
21070 X=312:Y=152
21075 GET (X,Y)-(X+7,Y+8),A%
21080 XD=X-XT:YD=Y-YT
21090 IF XD>YD THEN S=XD ELSE S=YD
21100 XI=XD/S:YI=YD/S
21110 FOR Z1=1 TO S
21120 PUT (X,Y),A%,PSET
21140 X=X-XI:Y=Y-YI
wait 200,0
21150 NEXT Z1
21153 PUT (XT,YT),A%,PSET
21155 XT=XT+8
21160 NEXT Z
21170 RETURN
